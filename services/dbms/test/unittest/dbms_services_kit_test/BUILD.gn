# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/test.gni")
import("../../../../../dbms.gni")

module_output_path = "bundle_framework/distributedbundleservice"

ohos_unittest("DbmsServicesKitTest") {
  use_exceptions = true
  module_out_path = module_output_path
  include_dirs = [
    "${dbms_inner_api_path}/include",
    "${dbms_services_path}/include",
    "${bundle_framework_path}/services/bundlemgr/include",
  ]

  sources = [
    "${dbms_inner_api_path}/src/distributed_bms_proxy.cpp",
    "${dbms_services_path}/src/account_manager_helper.cpp",
    "${dbms_services_path}/src/dbms_device_manager.cpp",
    "${dbms_services_path}/src/distributed_bms.cpp",
    "${dbms_services_path}/src/distributed_bms_host.cpp",
    "${dbms_services_path}/src/distributed_data_storage.cpp",
  ]

  sources += [ "dbms_services_kit_test.cpp" ]

  deps = [
    "${dbms_inner_api_path}:dbms_fwk",
    "${dbms_services_path}/test/sceneProject/unittest/ohos_test:copy_ohos_test",
    "${dbms_services_path}/test/sceneProject/unittest/system_module:distributed_system_module",
  ]

  external_deps = [
    "ability_base:want",
    "access_token:libaccesstoken_sdk",
    "access_token:libnativetoken",
    "access_token:libtoken_setproc",
    "access_token:libtokenid_sdk",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "bundle_framework:libappexecfwk_common",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "device_manager:devicemanagersdk",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "i18n:intl_util",
    "init:libbegetutil",
    "ipc:ipc_core",
    "kv_store:distributeddata_inner",
    "resource_management:global_resmgr",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]
  defines = []
  if (current_cpu == "arm64") {
    defines += [ "ON_64BIT_SYSTEM" ]
  }
  if (hisysevent_enable_dbms) {
    sources += [ "${dbms_services_path}/src/event_report.cpp" ]
    external_deps += [ "hisysevent:libhisysevent" ]
    defines += [ "HISYSEVENT_ENABLE" ]
  }

  if (account_enable_dbms) {
    external_deps += [ "os_account:os_account_innerkits" ]
    defines += [ "ACCOUNT_ENABLE" ]
  }

  if (distributed_bundle_image_framework_enable) {
    sources += [ "${dbms_services_path}/src/image_compress.cpp" ]
    external_deps += [ "image_framework:image_native" ]
    defines += [ "DISTRIBUTED_BUNDLE_IMAGE_ENABLE" ]
  }
}

group("unittest") {
  testonly = true
  if (ability_runtime_enable_dbms &&
      distributed_bundle_image_framework_enable) {
    deps = [ ":DbmsServicesKitTest" ]
  }
}
